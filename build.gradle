plugins {
	id 'java'

	id 'com.github.johnrengelman.shadow' version '4.0.4'
	id 'com.github.hierynomus.license' version '0.15.0'
	id 'nebula.info' version '5.0.3'
	id 'checkstyle'
	id 'findbugs'
	id 'pmd'

	id 'com.github.ben-manes.versions' version '0.21.0'
}

def os = System.getProperty("os.name").toLowerCase(Locale.ENGLISH)

apply from: 'gradle/dependencyIndicies.gradle'
if (os.contains("mac") || os.contains("darwin")) {
	apply from: 'gradle/macos.gradle'
}

configurations {
	appbundler
}

repositories {
	mavenCentral()
	jcenter()
	maven { url 'https://libraries.minecraft.net' }
}

dependencies {
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

	compile group: 'com.mojang', name: 'authlib', version: '1.5.22'

	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
	compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.8'
	compile group: 'club.minnced', name: 'java-discord-rpc', version: '2.0.2'
	compile group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.4.0'
	compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '5.0.4'
	compile group: 'com.github.vatbub', name: 'mslinks', version: '1.0.5'

	shadow files('run/jcef/jcef.jar')
	appbundler files('lib/appbundler-1.0ea.jar')
}

jar {
	manifest {
		attributes(
				'Built-By': System.properties['user.name'],
				'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
				'Implementation-Name': project.name,
				'Implementation-Version': project.version,
				'Implementation-Vendor': project.url,
				'Class-Path': 'jcef/jcef.jar',
				'Main-Class': 'de.myftb.launcher.Launcher'
		)
	}
}

/* ======================================== Buildscript Configuration ======================================== */

build.dependsOn shadowJar

sourceCompatibility = targetCompatibility = '1.8'

sourceSets.main.resources.srcDirs += 'src/main/ui/build'

configure([compileJava, compileTestJava]) {
	options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
	options.deprecation = true
	options.encoding = 'UTF-8'
}

/*sourceSets {
	main {
		java {
			srcDirs = ['build/src/main/java']
		}
	}
}*/


task prepareSources(type: Copy) {
	from('src/main/java')
	into('build/src/main/java')
	filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: project.version])
}

task buildWebroot(type: Exec) {
	workingDir "$projectDir/src/main/ui"

	if (os.contains('windows')) {
		commandLine 'cmd', '/c', 'node_modules\\.bin\\webpack.cmd', '--mode', 'production'
	} else {
		commandLine './node_modules/.bin/webpack', '--mode', 'production'
	}
}
processResources.dependsOn buildWebroot

compileJava {
	dependsOn prepareSources
}

task sourceJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourceJar, jar, shadowJar
}

license {
	ext {
		name = project.name
		organization = project.organization
		url = project.url
		year = Calendar.getInstance().get(Calendar.YEAR)
	}

	header project.file('config/HEADER.txt')
	include '**/*.java'

	ignoreFailures false
	strictCheck true

	mapping {
		java = 'SLASHSTAR_STYLE'
	}
}

processResources {
	from 'LICENSE.txt'
}

checkstyle {
	toolVersion = '8.18'
	configDir = project.file('config/')
	configProperties = [
			'basedir': project.projectDir,
			'suppressions': project.file('config/checkstyle-suppressions.xml')
	]
}

findbugs {
	toolVersion = '3.0.1'
	ignoreFailures = true
	sourceSets = [sourceSets.main]
	reportsDir = file("$project.buildDir/reports/findbugs")
	effort = 'max'
}

pmd {
	ignoreFailures = true
	sourceSets = [sourceSets.main]
	reportsDir = file("$project.buildDir/reports/pmd")
	ruleSets = ['java-basic', 'java-braces']
}